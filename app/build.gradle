import java.text.SimpleDateFormat
import org.ajoberstar.grgit.Grgit

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'org.ajoberstar.release-opinion'
apply from: '../config/quality.gradle'

release {
    grgit = Grgit.open(project.file('..'))
}

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "org.connectbot"

        minSdkVersion 4
        targetSdkVersion 15

        versionCode 365
        versionName "${project.version.toString()}"

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_5
            targetCompatibility JavaVersion.VERSION_1_5
        }

        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

        ndk {
            moduleName "com_google_ase_Exec"
            ldLibs "log"
        }

        lintOptions {
            abortOnError false
            lintConfig file('lint.xml')
        }

        testApplicationId "org.connectbot.tests"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    signingConfigs {
        releaseSigning
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles 'proguard.cfg'

            if (project.hasProperty('keystorePassword')) {
                signingConfig signingConfigs.releaseSigning
            }
        }

        debug {
            applicationIdSuffix ".debug"
        }
    }
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'");
    df.setTimeZone(TimeZone.getTimeZone("UTC"));
    return df.format(new Date());
}

if (project.hasProperty('keystorePassword')) {
    android.signingConfigs.releaseSigning.storeFile file(keystoreFile)
    android.signingConfigs.releaseSigning.storePassword keystorePassword
    android.signingConfigs.releaseSigning.keyAlias keystoreAlias
    android.signingConfigs.releaseSigning.keyPassword keystorePassword
}
